apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def getAppBuildNumber = { ->
    return project.properties.getOrDefault("buildNumber", 0).toInteger()
}

def getAppVersionCode = { ->
    def maj = (1000000 * deps.build.version_major)
    def min = (100000 * deps.build.version_minor)
    def patch = (10000 * deps.build.version_patch)
    def result = maj + min + patch + getAppBuildNumber()

    logger.lifecycle("### Version code: " + result + "###")
    return result
}

def getAppVersionName = { ->
    def result = "${deps.build.version_major}.${deps.build.version_minor}.${deps.build.version_patch}"
    logger.lifecycle("### Version name: " + result + "###")
    return result
}

android {

    // Compile and build tools versions:
    compileSdkVersion deps.build.target_sdk
    buildToolsVersion deps.build.build_tools

    defaultConfig {

        // App id:
        applicationId deps.build.application_group

        // Min and target sdk:
        minSdkVersion deps.build.min_sdk
        targetSdkVersion deps.build.target_sdk

        // Version name and code:
        versionName getAppVersionName()
        versionCode getAppVersionCode()

        // Use multidex.
        multiDexEnabled true

        // Support vector drawables.
        vectorDrawables.useSupportLibrary = true

        // Test runner.
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }



    buildTypes {

        // Release build:
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        // Debug build:
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['release']
        }
    }

    // Compile with java 8.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Enable databinding.
    dataBinding.enabled = true

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res_main',
                    'src/main/res_launcher',
                    'src/main/res_bev_drawables'
            ]
        }
    }
}

dependencies {

    // Local jars:
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation(

            // Kotlin:
            deps.kotlin.lib,

            // Support:
            deps.support.appcompat,
            deps.support.constraintlayout,

            // Nav arch:
            deps.nav.fragment,
            deps.nav.ui,

            // Firebase:
            deps.firebase.core,
            deps.firebase.database
    )

    implementation 'com.android.support:support-v4:28.0.0'
    testImplementation deps.junit.lib
    androidTestImplementation deps.support.testrunner
    androidTestImplementation deps.support.espresso
}

apply plugin: 'com.google.gms.google-services'
