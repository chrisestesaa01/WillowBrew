apply plugin: "com.github.ben-manes.versions"

buildscript {

    apply from: 'dependencies.gradle'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath deps.plugins.gradle
        classpath deps.plugins.kotlin
        classpath deps.plugins.google
        classpath deps.plugins.dependencyUpdate
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Run dependency check before lint.
task lint(dependsOn : dependencyUpdates)

// Don't count alpha, beta, rc, etc. releases when checking for dependency updates.
dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
